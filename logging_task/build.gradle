plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.1'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.example.scdf.task'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation platform("org.springframework.cloud:spring-cloud-task-dependencies:3.2.0")
	implementation 'org.springframework.cloud:spring-cloud-starter-task'
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	// H2 Database is required to run the application locally.
	// This is required for unit test execution.
	runtimeOnly 'com.h2database:h2'
	// Requires a driver for the DB used for SCDF management.
	implementation 'org.postgresql:postgresql:42.7.4'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
	// Mockito is currently self-attaching to enable the inline-mock-maker. This will no longer work in future releases of the JDK.
	// Please add Mockito as an agent to your build what is described in Mockito's documentation:
	// https://javadoc.io/doc/org.mockito/mockito-core/latest/org/mockito/Mockito.html#0.3
	jvmArgs += ['-Xshare:off', "-javaagent:${configurations.testRuntimeClasspath.find { it.name.contains('mockito-core') }}"]
}

jar {
	enabled = false
}

bootJar {
	enabled = true
}
